def manifestConfiguration = {
    manifest {
        attributes(
                [
                        "Specification-Title"     : project.name,
                        "Specification-Vendor"    : "cypher121",
                        "Specification-Version"   : version,
                        "Implementation-Title"    : project.name,
                        "Implementation-Version"  : version,
                        "Implementation-Vendor"   : "Cypher121",
                        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
                ]
        )
    }
}

tasks.register('javadocJar', Jar) {
    group = 'build'
    description = 'Creates a Javadoc artifact'
    
    classifier = 'javadoc'
    from javadoc.destinationDir

    dependsOn genJavadoc
}

tasks.register('libOnlyJar', Jar) {
    group = 'build'
    description = 'Creates an artifact with mod definitions stripped out'

    configure manifestConfiguration

    classifier = 'lib'
    from sourceSets.main.output
    exclude('**/mods.toml', '**/Kettle.class')
}

tasks.register('sourceJar', Jar) {
    group = 'build'
    description = 'Creates a source code artifact'
    
    configure manifestConfiguration

    classifier = 'src'
    from sourceSets.main.allSource
}

configure(jar, manifestConfiguration)

assemble.dependsOn(javadocJar, libOnlyJar, sourceJar)