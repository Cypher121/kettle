import com.jfrog.bintray.gradle.BintrayPlugin
import com.matthewprenger.cursegradle.CurseGradlePlugin

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
            jcenter()
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.2.0'
    }
}

apply plugin: CurseGradlePlugin
apply plugin: BintrayPlugin

apply plugin: 'signing'
apply plugin: 'maven-publish'

ext.releaseClassifier = versionComponents.size() < 3 ? '' : "-${versionComponents[2]}"

if (releaseClassifier.contains('beta') || releaseClassifier.contains('rc')) {
    ext.buildType = 'beta'
} else if (releaseClassifier.contains('alpha')) {
    ext.buildType = 'alpha'
} else {
    ext.buildType = 'release'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            artifact javadocJar
            artifact libOnlyJar
            artifact sourceJar

            pom {
                name = 'Kettle'
                description = 'Kotlin utilities for Minecraft Forge mod development'
                url = 'https://minecraft.curseforge.com/projects/kettle'

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'cypher121'
                        name = 'Cypher121'
                        email = 'ds98s3a@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Cypher121/kettle.git'
                    developerConnection = 'scm:git:ssh://github.com/Cypher121/kettle.git'
                    url = 'https://github.com/Cypher121/kettle/'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: ''
    key = System.getenv('BINTRAY_KEY') ?: ''

    publications = ['maven']

    pkg {
        repo = 'minecraft'
        name = 'Kettle'
        userOrg = 'cypher121'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Cypher121/kettle.git'
        labels = ['minecraft', 'minecraftforge', 'kotlin']

        version {
            released = new Date()

            name = project.version
            desc = "$project.name $project.version"
            vcsTag = project.version

            gpg {
                sign = true

                if (project.hasProperty('signing.password')) {
                    passphrase = project.getProperty('signing.password')
                }
            }
        }
    }
}

curseforge {
    apiKey = System.getenv('CURSEFORGE_API_KEY') ?: ''

    project {
        id = '316905'
        releaseType = buildType

        mainArtifact tasks.getByName('jar')
        addArtifact tasks.getByName('libOnlyJar')
        addArtifact tasks.getByName('sourceJar')
        addArtifact tasks.getByName('javadocJar')
    }
}

signing {
    ext.'signing.password' = System.getenv('SIGNING_PASSWORD') ?: ''
    ext.'signing.keyId' = System.getenv('SIGNING_KEY_ID') ?: ''
    ext.'signing.secretKeyRingFile' = System.getenv('SIGNING_KEYRING_LOCATION') ?: ''

    required {
        gradle.taskGraph.allTasks.any { it.name.contains('publish') || it.name.contains('curseforge') }
    }

    sign publishing.publications.maven

    println(properties.keySet())
}

tasks.register('release') {
    group = 'publishing'
    description = 'Perform release across all registered platforms'

    dependsOn build

    doLast {
        def git = grgit.open()

        if (git.describe() != "v$project.version") {
            throw IllegalStateException("Current release tag '${git.describe()} does not match the version specified in properties '$project.version'")
        }
    }

    finalizedBy 'publish', 'bintrayPublish', 'curseforge'
}